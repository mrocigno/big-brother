// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.android.kotlin apply false
    alias libs.plugins.ksp apply false
    alias libs.plugins.medivh apply false
    alias libs.plugins.serialization apply false
    alias libs.plugins.deeplink apply false
}

allprojects {

    def isApp = project.name == 'app'
    def suffix = (isApp? '' : ".${project.name}")

    afterEvaluate {

        if (!project.hasProperty("android") || project.name.contains("plugin")) return

        android {
            namespace "br.com.mrocigno.bigbrother$suffix"
            compileSdk 34

            defaultConfig {
                minSdk 23
                targetSdk 34
                versionCode 1
                versionName project.findProperty("libVersion")
                resConfigs "en", "pt"

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                if (project.hasProperty("ksp")) {
                    ksp {
                        arg("room.schemaLocation", "$projectDir/schemas")
                    }
                }
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }

            kotlinOptions {
                jvmTarget = '17'
            }

            buildFeatures {
                buildConfig = true
            }
        }

        dependencies {
            implementation libs.androidx.core
            implementation libs.androidx.appcompat
            implementation libs.material
        }
    }
}