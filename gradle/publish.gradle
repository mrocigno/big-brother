apply plugin: 'maven-publish'
apply plugin: 'signing'

def libGroup = "io.github.mrocigno"
def libVersion = "0.0.6-SNAPSHOT"

android {

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {

        maven(MavenPublication) {
            groupId = libGroup
            artifactId = "big-brother-${project.name}"
            version = libVersion

            afterEvaluate {
                from components.release
            }

            pom {

                name = 'BigBrother'
                description = 'Think on a better description'
                url = 'https://github.com/mrocigno/big-brother'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mrocigno'
                        name = 'Matheus Medeiros'
                        email = 'rocignom@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/mrocigno/big-brother'
                    connection = "scm:https://github.com/mrocigno/big-brother.git"
                    developerConnection = 'scm:git@github.com:mrocigno/big-brother.git'
                }
            }
        }
    }

    repositories {
        maven {
            if (libVersion.containsIgnoreCase("SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            credentials {
                username = System.getenv("ossrh_username") ?: ossrh_username
                password = System.getenv("ossrh_password") ?: ossrh_password
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}