apply plugin: 'maven-publish'
apply plugin: 'signing'

def libGroup = "io.github.mrocigno"
def libVersion = "0.0.3-SNAPSHOT"

android {

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {

        maven(MavenPublication) {
            groupId = libGroup
            artifactId = "sandman-${project.name}"
            version = libVersion

            afterEvaluate {
                from components.release
            }

            pom {

                name = 'Sandman'
                description = 'Think on a better description'
                url = 'https://github.com/mrocigno/Sandman'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = developer_id
                        name = developer_name
                        email = developer_email
                    }
                }
                scm {
                    url = 'https://github.com/mrocigno/Sandman'
                    connection = "scm:https://github.com/mrocigno/Sandman.git"
                    developerConnection = 'scm:git@github.com:mrocigno/Sandman.git'
                }
            }
        }
    }

    repositories {
        maven {
            if (libVersion.containsIgnoreCase("SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            credentials {
                username = ossrh_username
                password = ossrh_password
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}